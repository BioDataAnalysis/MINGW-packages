# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>
# Contributor: wirx6 <wirx654@gmail.com>

_realname=Vulkan-LoaderAndValidationLayers
pkgbase=mingw-w64-vulkan
pkgname=("${MINGW_PACKAGE_PREFIX}-vulkan")
conflicts=("${MINGW_PACKAGE_PREFIX}-vulkan-headers")
replaces=("${MINGW_PACKAGE_PREFIX}-vulkan-headers")
pkgver=1.0.46.0
pkgrel=1
pkgdesc='Khronos official ICD loader and validation layers for Vulkan (mingw-w64)'
arch=('any')
url="https://www.khronos.org/vulkan/"
groups=("${MINGW_PACKAGE_PREFIX}-vulkan-devel")
license=('Apache')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-glslang"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-spirv-tools")
source=(${_realname}-sdk-${pkgver}.tar.gzls::"https://github.com/KhronosGroup/${_realname}/archive/sdk-${pkgver}.tar.gz"
        001-build-fix.patch
        002-proper-def-files-for-32bit.patch
        003-generate-pkgconfig-files.patch
        004-installation-commands.patch)
sha256sums=('439db567f03448f57d7899c4a78986669dc4d930af9699986a85bf2fc3d3acb8'
            'ef294ad7279c726d203d70c014bfa8814b1b20de81d7bca3b2730229868e29f7'
            '3a0ce08d5b1b59a4661d770f6da71d1fb1f9805a3f5f670766c5202d1a50756c'
            '350b1a952146e17d280b43f89a489fb95f076639986fcf98b43cb585a8a886a3'
            '0ee6579c4fa2f91b39ee623f6efee92d46223faf3f99afd9f554ce101f045d81')

prepare() {
  cd ${srcdir}/${_realname}-sdk-${pkgver}
  patch -p1 -i ${srcdir}/001-build-fix.patch
  patch -p1 -i ${srcdir}/002-proper-def-files-for-32bit.patch
  patch -p1 -i ${srcdir}/003-generate-pkgconfig-files.patch
  patch -p1 -i ${srcdir}/004-installation-commands.patch
}

build() {
  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}

  CFLAGS=$CFLAGS" -D_WIN32_WINNT=0x0600" \
  CPPFLAGS=$CPPCFLAGS" -D_WIN32_WINNT=0x0600" \
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G'MSYS Makefiles' \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -fpermissive -D_WIN32_WINNT=0x0600" \
    -DBUILD_DEMOS=OFF \
    -DBUILD_TESTS=OFF \
    ../${_realname}-sdk-${pkgver}

  make -j1
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR="${pkgdir}" install

  install -Dm644 ${srcdir}/${_realname}-sdk-${pkgver}/LICENSE.TXT ${pkgdir}${MINGW_PREFIX}/share/licenses/vulkan/LICENSE
}
