# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=vulkan
pkgbase=mingw-w64-${_realname}-docs
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-man-pages" "${MINGW_PACKAGE_PREFIX}-${_realname}-html-docs")
_pkgname=Vulkan-Docs
pkgver=1.0.51
_pkgver=${pkgver}-core
epoch=1
pkgrel=1
arch=('any')
url="https://www.khronos.org/vulkan/"
groups=("${MINGW_PACKAGE_PREFIX}-vulkan-devel")
makedepends=("asciidoc" "git" "python3")
license=('custom')
source=(${_realname}-docs-${_pkgver}.tar.gz::"https://github.com/KhronosGroup/${_pkgname}/archive/v${_pkgver}.tar.gz")
sha256sums=('64e5ef8e4a34de6c3723648d1993f111cb2d6edb533dab4f5dd55d974083f4a7')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}-core/doc/specs/vulkan"
  #./makeKHR manpages
  # Broken, see https://github.com/KhronosGroup/Vulkan-Docs/issues/367
  # workaround :( (yes, twice the same command)
  LANG="en_US.UTF8" make allman
  LANG="en_US.UTF8" make allman
  ./makeKHR chunked
}

package_vulkan-man-pages() {
  pkgdesc="Vulkan man pages (mingw-w64)"
  cd "${srcdir}/${_pkgname}-${_pkgver/+/-}"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/man/man3"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-man-pages"

  install -m644 out/1.0/man/3/*.3 "${pkgdir}${MINGW_PREFIX}/share/man/man3/"

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-man-pages/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-man-pages/copyright-spec.txt
}

package_vulkan-html-docs() {
  pkgdesc="Vulkan html documentation (mingw-w64)"
  cd "${srcdir}/${_pkgname}-${pkgver}-core"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/doc/vulkan"
  install -dm755 "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-docs"

  mv -v out/1.0/chunked/* "${pkgdir}${MINGW_PREFIX}/share/doc/vulkan/"

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-docs/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}-docs/copyright-spec.txt
}

package_mingw-w64-x86_64-vulkan-man-pages() {
  package_vulkan-man-pages
}

package_mingw-w64-i686-vulkan-man-pages() {
  package_vulkan-man-pages
}

package_mingw-w64-x86_64-vulkan-html-docs() {
  package_vulkan-html-docs
}

package_mingw-w64-i686-vulkan-html-docs() {
  package_vulkan-html-docs
}
